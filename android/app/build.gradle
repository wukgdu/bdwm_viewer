plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "com.wukgdu365.bdwm_viewer"
    // compileSdkVersion flutter.compileSdkVersion
    compileSdkVersion 34
    // ndkVersion flutter.ndkVersion
    ndkVersion "26.2.11394342"

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = 17
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "com.wukgdu365.bdwm_viewer"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-build-configuration.
        // minSdkVersion flutter.minSdkVersion
        minSdkVersion 23
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
    }

    signingConfigs {
        release {
           keyAlias keystoreProperties['keyAlias']
           keyPassword keystoreProperties['keyPassword']
           storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
           storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
        }
        release {
            // Signing with the debug keys for now, so `flutter run --release` works.
            // signingConfig signingConfigs.debug
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
        }
    }

    splits {
        abi {
            enable true
            reset()
            universalApk true  // If true, also generate a universal APK
            include "arm64-v8a", "armeabi-v7a", "x86_64"
            exclude "x86"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def buildType = variant.name
            def version = variant.versionName
            def createTime = new Date().format('yyyyMMddHHmmss', TimeZone.getTimeZone("GMT+08:00"))
            def abi = output.getFilter(com.android.build.OutputFile.ABI)
            // if (buildType == "debug") {
            // } else if (buildType == "release") {
            // }
            if (abi != null) {
                outputFileName = "OBViewer_${buildType}_${version}_${abi}_${createTime}.apk"
            } else {
                outputFileName = "OBViewer_${buildType}_${version}_${createTime}.apk"
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.23"
    // implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.23"
    // https://mvnrepository.com/artifact/com.android.tools/desugar_jdk_libs
    // implementation group: 'com.android.tools', name: 'desugar_jdk_libs', version: '2.0.4'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2' // 1.1.5 ? 1.2.2 ? 2.0.4
    // https://github.com/flutter/flutter/issues/110658 https://stackoverflow.com/a/74410353/17742614
    implementation 'androidx.window:window:1.0.0'
    implementation 'androidx.window:window-java:1.0.0'
}
